apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.adamin.manslove"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 4
        versionName "0.6.2 beta"

        // dex突破65535的限制
        multiDexEnabled true
        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }
    lintOptions {
        abortOnError false
    }

    signingConfigs {
//        debug {
//            // No debug config
//        }

        release {
            storeFile file("D:\\develope\\keystore\\litao.jks")
            storePassword "adam1990"
            keyAlias "litao"
            keyPassword "adam1990"
        }
    }
    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"

            minifyEnabled false //不混淆
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为boohee_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "ManLove_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }
    productFlavors {

        wandoujia {}
        baidu {}
        c360 {}
        uc {}
        huawei {}
        tengxun {}

        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }

    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.squareup.okhttp3:okhttp:3.1.2'
    compile 'com.zhy:okhttputils:2.2.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.nineoldandroids:library:2.4.0'
    compile project(':superrecyclerview')
    compile project(':adamintoolkits')
    compile 'com.google.code.gson:gson:2.6.1'
    compile 'com.makeramen:roundedimageview:2.2.1'
    compile 'com.commit451:PhotoView:1.2.4'
    compile 'com.github.castorflex.smoothprogressbar:library-circular:1.2.0'
    compile 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.android.support:design:23.2.0'
    compile 'com.android.support:cardview-v7:23.2.0'
    compile 'com.android.support:recyclerview-v7:23.2.0'
    compile 'com.android.support:palette-v7:23.2.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile files('src/main/libs/umeng-update-v2.6.0.1.jar')
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.4'
}
